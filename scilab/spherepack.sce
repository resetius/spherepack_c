
spherepack_initialized=0;

function initspherepack()
    if (spherepack_initialized <> 1) then
        link("spherepack", [
        "shaeci",
        "vhseci",
        "shseci",
        "vhaeci",
        
        "shaec",
        "shsec",
        
        "slapec",
        "islapec",
        "islapec_l3",
        
        "vhaec",
        "divec",
        "gradec",
        "igradec"
        ])
        spherepack_initialized = 1
    end
endfunction

function swsave = shaeci(nlat, nlon)
    [swsave, sdwork, ierror] = call("shaeci",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        5*nlat*nlat*nlon, 4, "i",...
        4*nlat*nlat, 6, "i",...
        'out', ...
        [5*nlat*nlat*nlon,1], 3, "d", ...
        [4*nlat*nlat,1], 5, "d",...
        [1,1], 7, "i")

    if (ierror <> 0) then
        error(msprintf("error in shaeci: %d", ierror))
    end
endfunction

function vwsave = vhseci(nlat, nlon)
    vwsave = zeros(5*nlat*nlat*nlon,1)
    vdwork = zeros(4*nlat*nlat,1)

    [vwsave, vdwork, ierror] = call("vhseci",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        size(vwsave, 1), 4, "i",...
        size(vdwork, 1), 6, "i",...
        'out', ...
        size(vwsave), 3, "d", ...
        size(vdwork), 5, "d",...
        [1,1], 7, "i")

    if (ierror <> 0) then
        error(msprintf("error in vhseci: %d", ierror))
    end
endfunction

function ivwsave = vhaeci(nlat, nlon)
    ivwsave = zeros(5*nlat*nlat*nlon,1)
    ivdwork = zeros(4*nlat*nlat,1)
    
    [ivwsave, ivdwork, ierror] = call("vhaeci", ...
        nlat, 1, "i",...
        nlon, 2, "i",...
        size(ivwsave, 1), 4, "i",...
        size(ivdwork, 1), 6, "i",...
        'out', ...
        size(ivwsave), 3, "d", ...
        size(ivdwork), 5, "d",...
        [1,1], 7, "i")

    if (ierror <> 0) then
        error(msprintf("error in vhaeci: %d", ierror))
    end
endfunction

function iswsave = shseci(nlat, nlon)
    iswsave = zeros(5*nlat*nlat*nlon,1)
    isdwork = zeros(4*nlat*nlat,1)

    [iswsave, isdwork, ierror] = call("shseci",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        size(iswsave, 1), 4, "i",...
        size(isdwork, 1), 6, "i",...
        'out', ...
        size(iswsave), 3, "d", ...
        size(isdwork), 5, "d",...
        [1,1], 7, "i")
endfunction

function [u, v] = shaec(ff, swsave)
    if (size(size(ff)) <> 2) then
        error("size mismatch (1)")
    end

    if (size(size(swsave)) <> 2) then
        error("size mismatch (2)")
    end

    [nlat, nlon]=size(ff)
    mdab = min(nlat, int((nlon + 2) / 2))

    u=zeros(nlat,mdab)
    v=zeros(nlat,mdab)

    work = zeros(5*nlat*nlat*nlon,1)
    isym = 0
    nt = 1
    ierror = 0

    [u, v, work, ierror] = call("shaec",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        isym, 3, "i",...
        nt, 4, "i",...
        ff, 5, "d", ...
        nlat, 6, "i",...
        nlon, 7, "i",...
        mdab, 10, "i",...
        nlat, 11, "i",...
        swsave, 12, "d",...
        size(swsave,1),13,"i",...
        work,14,"d",...
        size(work,1),15,"i",...
        'out', ...
        size(u), 8, "d", ...
        size(v), 9, "d",...
        size(work), 14, "d",...
        [1,1], 16, "i")

    if (ierror <> 0) then
        error(msprintf("error in shaec: %d", ierror))
    end
endfunction

function [br, bi, cr, ci] = vhaec(u, v, ivwsave)
    if (size(u) <> size(v)) then
        error("size mismatch (1)")
    end
    if (size(size(u)) <> 2) then
        error("size mismatch (2)")
    end
    
    [nlat, nlon] = size(u)

    ityp = 0
    nt = 1
    mdc = min(nlat, int((nlon + 2) / 2))

    work = zeros(5*nlat*nlat*nlon, 1)

    br = zeros(mdc, nlat*nt)
    bi = zeros(mdc, nlat*nt)
    cr = zeros(mdc, nlat*nt)
    ci = zeros(mdc, nlat*nt)

    [br, bi, cr, ci, work, ierror] = call("vhaec",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        ...
        ityp, 3, "i",...
        nt, 4, "i",...
        u, 5, "d",...
        v, 6, "d",...
        nlat, 7, "i",...
        nlon, 8, "i",...
        ...
        mdc, 13, "i",...
        nlat, 14, "i",...
        ...
        ivwsave, 15, "d",...
        size(ivwsave, 1), 16, "i",...
        ...
        size(work, 1), 18, "i",...
        ...
        'out',...
        size(br), 9, "d",...
        size(bi), 10, "d",...
        size(cr), 11, "d",...
        size(ci), 12, "d",...
        size(work), 17, "d",...
        [1,1], 19, "i")
        
    if (ierror <> 0) then
        error(msprintf("error in vhaec: %d", ierror))
    end
endfunction

function f = shsec(a, b, nlat, nlon, iswsave)
    nt = 1
    isym = 0
    work = zeros(5*nlat*nlat*nlon,1)
    mdab = min(nlat, int((nlon + 2) / 2))
    [f, work, ierror] = call("shsec", ...
        nlat, 1, "i",...
        nlon, 2, "i",...
        isym, 3, "i",...
        nt, 4, "i",...
        nlat, 6, "i",...
        nlon, 7, "i",...
        a, 8, "d",...
        b, 9, "d",...
        mdab, 10, "i",...
        nlat, 11, "i",...
        iswsave, 12, "d",...
        size(iswsave, 1), 13, "i",...
        size(work, 1), 15, "i",...
        "out",...
        [nlat, nlon], 5, "d",...
        size(work), 14, "d",...
        [1,1], 16, "i"...
    )
    if (ierror <> 0) then
        error(msprintf("error in shsec: %d", ierror))
    end
endfunction

function vt = divec(br, bi, iswsave, nlat, nlon)
    nt = 1
    mdc = min(nlat, int((nlon + 2) / 2))
    isym = 0
    
    work = zeros(5*nlat*nlat*nlon,1)

    [vt, work, ierror] = call("divec",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        isym, 3, "i",...
        nt, 4, "i",...
        nlat, 6, "i",...
        nlon, 7, "i",...
        br, 8, "d",...
        bi, 9, "d",...
        mdc, 10, "i",...
        nlat, 11, "i",...
        iswsave, 12, "d",... 
        size(iswsave, 1), 13, "i",...
        size(work,1), 15, "i",...
        'out',...
        [nlat,nlon],5,"d",...
        size(work), 14, "d",...
        [1,1], 16, "i")
    if (ierror <> 0) then
        error(msprintf("error in divec: %d", ierror))
    end
endfunction

function [u, v] = gradec(a, b, nlat, nlon, vwsave)
    isym = 0
    nt = 1
    mdab = min(nlat, int((nlon + 2) / 2) )

    [u, v, work, ierror] = call("gradec", ...
        nlat, 1, "i",...
        nlon, 2, "i", ...
        isym, 3, "i", ...
        nt, 4, "i", ...
        ... //5, 6 = u, v out
        nlat, 7, "i",...
        nlon, 8, "i", ...
        a, 9, "d", ...
        b, 10, "d", ...
        mdab, 11, "i",...
        nlat, 12, "i",...
        vwsave, 13, "d", ...
        size(vwsave, 1), 14, "i",...
        ...//15 work
        5*nlat*nlat*nlon, 16, "i",...
        'out',...
        [nlat, nlon], 5, "d",...
        [nlat, nlon], 6, "d",...
        [5*nlat*nlat*nlon,1], 15, "d",...
        [1,1], 17, "i")

    if (ierror <> 0) then
        error(msprintf("error in gradec: %d", ierror))
    end
endfunction

function lap = slapec(a, b, nlat, nlon, iswsave)
    isym = 0
    nt = 1
    mdab = min(nlat, int((nlon + 2) / 2) )

    [lap, work, ierror] = call("slapec",...
        nlat, 1, "i",...
        nlon, 2, "i",...
        isym, 3, "i",...
        nt, 4, "i",...
        nlat, 6, "i", ...
        nlon, 7, "i", ...
        a, 8, "d", ...
        b, 9, "d", ...
        mdab, 10, "i", ...
        nlat, 11, "i", ...
        iswsave, 12, "d", ...
        size(iswsave, 1), 13, "i", ...
        5*nlat*nlat*nlon, 15, "i", ...
        'out',...
        [nlat,nlon],5,"d",...
        [5*nlat*nlat*nlon,1],14, "d",...
        [1,1],16,"i"...
    )
    if (ierror <> 0) then
        error(msprintf("error in slapec: %d", ierror))
    end
endfunction

// High Level
function [u, v] = grad(fun)
    [nlat, nlon]=size(fun)
    swsave = shaeci(nlat, nlon)
    vwsave = vhseci(nlat, nlon)
    [a, b] = shaec(fun, swsave)
    [u, v] = gradec(a, b, nlat, nlon, vwsave)
endfunction

function vt = div(u, v)
    if (size(u) <> size(v)) then
        error("size mismatch (1)")
    end
    if (size(size(u)) <> 2) then
        error("size mismatch (2)")
    end

    [nlat, nlon]=size(u)

    ivwsave = vhaeci(nlat, nlon)
    iswsave = shseci(nlat, nlon)
    [br, bi, cr, ci] = vhaec(u, v, ivwsave)
    vt = divec(br, bi, iswsave, nlat, nlon)    
endfunction

function lap = lapl(fun)
    [nlat, nlon] = size(fun)
    swsave = shaeci(nlat, nlon)
    iswsave = shseci(nlat, nlon)
    [a, b] = shaec(fun, swsave)
    lap = slapec(a, b, nlat, nlon, iswsave)
endfunction

function [a, b] = func2koef(src)
    [nlat, nlon] = size(src)
    swsave = shaeci(nlat, nlon)
    [a, b] = shaec(src, swsave)
endfunction

function f=koef2func(a, b, nlat, nlon)
    iswsave = shseci(nlat, nlon)
    f = shsec(a, b, nlat, nlon, iswsave)
endfunction
